--  GEM.LTE.Primitives - Core Algorithms for Laplace's Tidal Equation Modeling
--
--  This package body implements the mathematical core of the GEM-LTE climate model:
--
--  KEY ALGORITHMS:
--  1. IIR (Infinite Impulse Response) - Differential equation integrator
--     Models memory/persistence effects in ocean-atmosphere system
--
--  2. FIR (Finite Impulse Response) - Moving average filter  
--     Smooths time series data
--
--  3. Tide_Sum - Tidal forcing superposition
--     Computes sum of multiple periodic tidal constituents
--
--  4. LTE (Laplace's Tidal Equation) - Wave equation solver
--     Models atmospheric/oceanic response to forcing via modulated wave equation
--
--  5. Regression_Factors - Linear least-squares parameter fitting
--     Determines optimal amplitudes/phases for tidal constituents
--
--  MATHEMATICAL BACKGROUND:
--  - Uses long period astronomical tidal cycles (18.6yr, 8.85yr, etc.)
--  - Applies nonlinear modulation via Laplace's Tidal Equation
--  - Fits to observational climate index data (ENSO, IOD, etc.)

with Ada.Long_Float_Text_IO;
with Text_IO;
with Ada.Numerics.Long_Elementary_Functions;
with Ada.Exceptions;
with Ada.Numerics.Generic_Real_Arrays;
with GNAT.OS_Lib;

--  COMMENTED CODE: Matrix operations package
--  TODO: Can remove - was for experimental matrix-based regression approach
--  Current code uses specialized regression functions instead
--with GEM.Matrices;

package body GEM.LTE.Primitives is

   --  Configuration flags from environment variables
   Aliased_Period : constant Boolean := GEM.Getenv ("ALIAS", False);
   Min_Entropy : constant Boolean := GEM.Getenv ("METRIC", "") = "ME";
   Linear_Step : constant Boolean := GEM.Getenv ("STEP", True);
   Every_N_Line : constant Integer := GEM.Getenv ("EVERY", 1);
   Magnify : constant Integer := GEM.Getenv ("MAG", 1);
   Clip : constant Long_Float := GEM.Getenv ("CLIP", 0.0);
   Start_Year : constant Long_Float := GEM.Getenv ("CC_START", 0.0);
   End_Year : constant Long_Float := GEM.Getenv ("CC_END", 999_999_999.0);
   Sinc : constant Long_Float := GEM.Getenv ("SINC", 0.0);
   Custom_Tide : constant Boolean := GEM.Getenv ("CUSTOM", False);

   --  Ridge (Tikhonov/L2) penalty added to Regression_Coefficients' normal
   --  equations, shrinking the OLS-fit level/k0/per-mode amp-phase/trend/
   --  accel/annual coefficients toward zero instead of letting them freely
   --  absorb whatever the training window happens to contain. 0.0 (default)
   --  reproduces the exact unregularized behavior; RIDGE also improves the
   --  conditioning of Regressors_T * Regressors, so it should reduce
   --  "Singular result" failures as a side effect even at small values.
   Ridge_Lambda : constant Long_Float := GEM.Getenv ("RIDGE", 0.0);

   --  Returns true if using minimum entropy metric for optimization
   function Is_Minimum_Entropy return Boolean is
   begin
      return Min_Entropy;
   end Is_Minimum_Entropy;

   --  Reduce: Downsample time series by averaging every N points
   --  Used to coarsen data resolution for faster computation
   function Reduce
     (Raw : in Data_Pairs; -- Raw starts with line 1
      Every : in Positive) return Data_Pairs
   is
      Res : Data_Pairs (1 .. Raw'Length / Every) := (others => (0.0, 0.0));
   begin
      for I in Res'Range loop
         for J in I * Every .. I * Every + Every - 1 loop
            Res (I).Value := Res (I).Value + Raw (J - Every + 1).Value;
         end loop;
         Res (I).Value := Res (I).Value / Long_Float (Every);
         Res (I).Date := Raw (I * Every - Every / 2).Date;
      end loop;
      return Res;
   end Reduce;

   --  Expand: Upsample time series by linear interpolation
   --  Increases temporal resolution by factor of Mag
   function Expand
     (Raw : in Data_Pairs; -- Raw starts with line 1
      Mag : in Positive) return Data_Pairs
   is
      Res : Data_Pairs (1 .. Raw'Length * Mag) := (others => (0.0, 0.0));
      Stride : Integer := 1;
   begin
      for I in Raw'First .. Raw'Last - 1 loop
         for J in 0 .. Mag - 1 loop
            Res (Stride + J).Value :=
              Raw (I).Value +
              (Raw (I + 1).Value - Raw (I).Value) * Long_Float (J) /
                Long_Float (Mag);
            Res (Stride + J).Date :=
              Raw (I).Date +
              (Raw (I + 1).Date - Raw (I).Date) * Long_Float (J) /
                Long_Float (Mag);
         end loop;
         Stride := Stride + Mag;
      end loop;
      Res (Raw'Length * Mag).Value := Raw (Raw'Last).Value;
      Res (Raw'Length * Mag).Date := Raw (Raw'Last).Date;
      return Res;
   end Expand;

   function File_Lines (Name : String) return Integer is
      Data : Text_IO.File_Type;
      Count : Integer := 0;
   begin
      if Name = "" then
         return 0;
      end if;
      Text_IO.Open (File => Data, Mode => Text_IO.In_File, Name => Name);
      loop
         Text_IO.Skip_Line (Data);
         Count := Count + 1;
      end loop;
   exception
      when Text_IO.End_Error =>
         Text_IO.Close (File => Data);
         return Count;
      when Text_IO.Name_Error =>
         Text_IO.Put_Line (Name & " not found?");
         raise Text_IO.Name_Error;
      when E : others =>
         Text_IO.Put_Line (Ada.Exceptions.Exception_Information (E));
         return 0;
   end File_Lines;

   function Make_Data (Name : String) return Data_Pairs is
      Lines : Integer := File_Lines (Name);
      Data : Text_IO.File_Type;
      Date, Value : Long_Float;
      Arr : Data_Pairs (1 .. Lines);
      Val : Long_Float;
   begin
      if Lines = 0 then
         return Arr;
      end if;
      Text_IO.Open (File => Data, Mode => Text_IO.In_File, Name => Name);
      for I in 1 .. Lines loop
         Ada.Long_Float_Text_IO.Get (File => Data, Item => Date);
         Ada.Long_Float_Text_IO.Get (File => Data, Item => Value);
         Text_IO.Skip_Line (Data);
         -- Text_IO.Put_Line(Date'Img & " " & Value'Img);
         if Clip > 0.0 then
            if Value > Clip then
               Val := Clip;
            elsif Value < -Clip then
               Val := -Clip;
            else
               Val := Value;
            end if;
            Arr (I) := (Date, Val);
         else
            Arr (I) := (Date, Value);
         end if;
      end loop;
      Text_IO.Close (File => Data);
      if Every_N_Line = 1 then
         return Arr;
      else
         return Reduce (Arr, Every_N_Line);
      end if;
   exception
      when E : others =>
         Text_IO.Put_Line (Ada.Exceptions.Exception_Information (E));
         GNAT.OS_Lib.OS_Exit (0);
         return Arr;
   end Make_Data;

   --  IIR: Infinite Impulse Response filter (integrator/differential equation solver)
   --
   --  Implements a recursive filter that models accumulation and dissipation:
   --    Output[i] = Input[i] + lagA*Output[i-1] - Ramp
   --
   --  Where Ramp provides asymmetric damping (sign-dependent dissipation).
   --  This creates "memory" in the system, modeling ocean heat content persistence.
   --
   --  UNUSED PARAMETERS (Compiler Warning):
   --  - lagA, lagB: Reserved for higher-order feedback (never implemented)
   --  - iB, iC: Additional initial conditions (single iA proved sufficient)
   --  - mA, mB: Annual modulation parameters (modulation handled elsewhere)
   --
   --  RATIONALE: Interface designed for extensibility but simpler first-order
   --  implementation proved adequate. Keeping interface for API stability.
   --
   --  Parameters:
   --    lagA, lagB, lagC - Lag coefficients (feedback gains)
   --    iA, iB, iC - Initial conditions
   --    Start - Start time for forward integration
   --    mA, mB - Modulation parameters (for annual cycle effects)
   --
   --  Note: Can run backwards from Start point to create pre-history
   function IIR
     (Raw : in Data_Pairs; lagA, lagC : in Long_Float;
      iA : in Long_Float := 0.0;
      Start : in Long_Float := Long_Float'First)
      return Data_Pairs
   is
      Res : Data_Pairs := Raw;
      Start_Index : Integer;
      Ramp : Long_Float;
      Mem : Long_Float;

   begin
      if lagA > 1.0 then
         Mem := 1.0;
      elsif lagA < 0.0 then
         Mem := 0.0;
      else
         Mem := lagA;
      end if;
      for I in Raw'Range loop
         Start_Index := I;
         exit when Raw (I).Date > Start;
      end loop;

      -- The first few values are sensitive to prior information so can adjust
      -- these for better fits in the early part of the time series.
      Res (Start_Index).Value := iA;
      for I in Start_Index + 1 .. Raw'Last loop
         Ramp := Long_Float'Copy_Sign (lagC, Res (I - 1).Value);
         Res (I).Value := Raw (I).Value + Mem*Res (I - 1).Value - Ramp;
      end loop;

   -- If Start is inside the series, create "pre-history" by running
   -- backwards -- using the EXACT algebraic inverse of the forward step
   -- above, not a separate heuristic. Forward:
   --   Res(I) = Raw(I) + Mem*Res(I-1) - Copy_Sign(lagC, Res(I-1))
   -- Copy_Sign's dependence on the SIGN of the unknown Res(I-1) makes
   -- this piecewise rather than a single division, but each piece is
   -- closed-form and exactly invertible where it applies:
   --   assume Res(I-1) >= 0: Res(I-1) = (Res(I)-Raw(I)+lagC) / Mem
   --   assume Res(I-1) <  0: Res(I-1) = (Res(I)-Raw(I)-lagC) / Mem
   -- Exactly one candidate is self-consistent with its own sign
   -- assumption whenever the two candidates don't straddle zero (the
   -- usual case -- verified to machine precision, 9.3e-15, on a 1128-
   -- step synthetic round-trip test in iir_invariant_test.adb). This
   -- REPLACES an earlier heuristic ("Res(I-1) := -Raw(I-1) + Mem*Res(I)
   -- + Copy_Sign(lagC, Res(I))") that was a plausible-looking but NOT
   -- algebraically exact inverse -- confirmed by that same test, which
   -- caught a ~2.5-unit round-trip discrepancy under the old formula.
   --
   -- Two edge cases, both real and both handled explicitly rather than
   -- left to divide-by-zero or silent ambiguity:
   --   * Mem = 0.0 (lagA <= 0.0, an unusual fitted configuration): the
   --     forward step no longer depends on Res(I-1)'s MAGNITUDE at all
   --     (only Copy_Sign's SIGN), so it is genuinely not invertible for
   --     magnitude -- division would raise Constraint_Error. Falls back
   --     to the old heuristic's shape in this singular case only.
   --   * The two candidates straddle zero (both self-consistent, or
   --     neither is): a real, provable dead zone of half-width lagC
   --     around Res(I)-Raw(I) where the forward map is many-to-one, so
   --     no inverse can be exact by construction, not just by omission --
   --     falls back to averaging the two candidates. Not instrumented
   --     here (IIR's signature stays unchanged so no call site needs
   --     updating); iir_invariant_test.adb checks directly that this
   --     never triggers for representative synthetic and real-manifold
   --     cases, where lagC is tiny relative to Forcing's own amplitude.
   --     A region whose own fitted lagC is instead large relative to its
   --     typical Forcing amplitude would hit this often and should not
   --     trust backward extrapolation without re-checking.
      for I in reverse (Raw'First + 1) .. Start_Index loop
         if Mem <= 0.0 then
            Ramp := Long_Float'Copy_Sign (lagC, Res (I).Value);
            Res (I - 1).Value := -Raw (I - 1).Value + Mem*Res (I).Value + Ramp;
         else
            declare
               Cand_Pos : constant Long_Float :=
                 (Res (I).Value - Raw (I).Value + lagC) / Mem;
               Cand_Neg : constant Long_Float :=
                 (Res (I).Value - Raw (I).Value - lagC) / Mem;
            begin
               if Cand_Pos >= 0.0 then
                  Res (I - 1).Value := Cand_Pos;
               elsif Cand_Neg < 0.0 then
                  Res (I - 1).Value := Cand_Neg;
               else
                  Res (I - 1).Value := 0.5 * (Cand_Pos + Cand_Neg);
               end if;
            end;
         end if;
      end loop;

      return Res;
   end IIR;

   function Extend_Backward
     (D : in Data_Pairs; Target_First_Date : in Long_Float;
      Sampling_Per_Year : in Long_Float) return Data_Pairs
   is
      First_Date : constant Long_Float := D (D'First).Date;
   begin
      if Target_First_Date >= First_Date then
         return D;
      end if;
      declare
         N_Extra : constant Integer :=
           Integer (Long_Float'Ceiling
             ((First_Date - Target_First_Date) * Sampling_Per_Year));
         Result : Data_Pairs (D'First - N_Extra .. D'Last);
      begin
         for K in 0 .. N_Extra - 1 loop
            Result (D'First - N_Extra + K) :=
              (Date => First_Date - Long_Float (N_Extra - K) / Sampling_Per_Year,
               Value => 0.0);
         end loop;
         Result (D'First .. D'Last) := D;
         return Result;
      end;
   end Extend_Backward;


   --  FIR: Finite Impulse Response filter (3-point moving average/boxcar filter)
   --
   --  Smooths time series using weighted average of neighboring points:
   --    Output[i] = Behind*Input[i-1] + Current*Input[i] + Ahead*Input[i+1]
   --
   --  Typical usage: Behind=1, Current=2, Ahead=1 (normalized) for simple smoothing
   --
   --  Does NOT have memory (unlike IIR) - each output depends only on local input
   function FIR
     (Raw : in Data_Pairs; Behind, Current, Ahead : in Long_Float)
      return Data_Pairs
   is
      Res : Data_Pairs := Raw;
   begin
      for I in Raw'First + 1 .. Raw'Last - 1 loop
         Res (I).Value :=
           Behind * Raw (I - 1).Value + Current * Raw (I).Value +
           Ahead * Raw (I + 1).Value;
      end loop;
      return Res;
   end FIR;

   -- Amplifies a tidal time series with an impulse array (i.e. Dirac comb)
   function Amplify
     (Raw : in Data_Pairs; Offset, Ramp, Start : in Long_Float)
      return Data_Pairs
   is
      Res : Data_Pairs := Raw;
   begin
      for I in Raw'Range loop
         -- Res(I).Value := Offset + Raw(I).Value * Impulse(Raw(I).Date + Ramp*(Raw(I).Date-Start));
         Res (I).Value :=
           Raw (I).Value *
           Impulse
             (Raw (I).Date + Ramp * (Raw (I).Date - Start) +
              Offset / 1_000_000.0 * (Raw (I).Date - Start)**2);
      end loop;
      return Res;
   end Amplify;

   -- Conventional tidal series summation or superposition of cycles
   function Tide_Sum_Diff
     (Template : in Data_Pairs; Constituents : in Long_Periods_Amp_Phase;
      Periods : in Long_Periods; Ref_Time : in Long_Float := 0.0;
      Scaling : in Long_Float := 1.0; Cos_Phase : in Boolean := True;
      Year_Len : in Long_Float := Year_Length; Integ : in Long_Float := 0.0) return Data_Pairs
   is
      Pi : Long_Float := Ada.Numerics.Pi;
      Time : Long_Float;
      Res : Data_Pairs := Template;
      One : constant Long_Float := 1.0;
      Partition : constant Integer := 7; -- 4
   begin
      for I in Template'Range loop
         Time := Template (I).Date + Ref_Time;
         declare
            TF1, TF2 : Long_Float := 0.0;
            use Ada.Numerics.Long_Elementary_Functions;
         begin
            for J in Constituents'First .. Constituents'First + Partition loop
               declare
                  L : Amp_Phase renames Constituents (J);
                  Freq : Long_Float := Year_Len / Periods (J);
               begin
                  if Aliased_Period then
                     Freq := Long_Float'Remainder (Freq, One);
                  end if;
                  if Cos_Phase then
                     TF1 :=
                       TF1 +
                       L.Amplitude * (Cos (2.0 * Pi * Freq * Time + L.Phase)) - 
                         Integ * Freq * (Sin (2.0 * Pi * Freq * Time + L.Phase));
                  else
                     TF1 :=
                       TF1 +
                       L.Amplitude * (Sin (2.0 * Pi * Freq * Time + L.Phase)) + 
                         Integ * Freq * (Cos (2.0 * Pi * Freq * Time + L.Phase));
                  end if;
               end;
            end loop;
            for J in Constituents'First + Partition + 1 .. Constituents'Last
            loop
               declare
                  L : Amp_Phase renames Constituents (J);
                  Freq : Long_Float := Year_Len / Periods (J);
               begin
                  if Aliased_Period then
                     Freq := Long_Float'Remainder (Freq, One);
                  end if;
                  if Cos_Phase then
                     TF2 :=
                       TF2 +
                       L.Amplitude * (Cos (2.0 * Pi * Freq * Time + L.Phase)) - 
                         Integ * Freq * (Sin (2.0 * Pi * Freq * Time + L.Phase));
                  else
                     TF2 :=
                       TF2 +
                       L.Amplitude * (Sin (2.0 * Pi * Freq * Time + L.Phase)) +
                         Integ * Freq * (Cos (2.0 * Pi * Freq * Time + L.Phase));
                  end if;
               end;
            end loop;
            Res (I) := (Time, TF1 + TF2 + Scaling * TF1 * TF2);
         end;
      end loop;
      return Res;
   end Tide_Sum_Diff;

-- Conventional tidal series summation or superposition of cycles
   function Tide_Sum_Custom
     (Template : in Data_Pairs; Constituents : in Long_Periods_Amp_Phase;
      Periods : in Long_Periods; Ref_Time : in Long_Float := 0.0;
      Scaling : in Long_Float := 1.0; Cos_Phase : in Boolean := True;
      Year_Len : in Long_Float := Year_Length; Integ : in Long_Float := 0.0;
      Ext_Forcing : in Data_Pairs := Empty_Data;
      Ext_Factor : in Long_Float := 0.0; Ext_Phase : in Long_Float := 0.0;
      Ext_Amp : in Long_Float := 0.0) return Data_Pairs
   is
      Pi : Long_Float := Ada.Numerics.Pi;
      Time : Long_Float;
      Res : Data_Pairs := Template;
      D, A, T, DD, TD, FF, DPlain, N, F : Long_Float;
      TF : Long_Float;
      pragma Unreferenced
        (Integ, Scaling, Cos_Phase, Ext_Forcing, Ext_Factor, Ext_Phase,
         Ext_Amp);
   begin
      for I in Template'Range loop
         Time := Template (I).Date + Ref_Time;
         declare -- uses 18.6y and 173
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (2);
            Freq : Long_Float := Year_Len / Periods (2);
            HF : Amp_Phase renames Constituents (5);
            HP : Long_Float := Year_Len / Periods (5);
            NF : Amp_Phase renames Constituents (11);
            NP : Long_Float := Year_Len / Periods (11);
         begin
            D :=
              L.Amplitude *
              (Cos
                 (2.0 * Pi * Freq * Time + L.Phase +
                  HF.Amplitude * Cos (2.0 * Pi * HP * Time + HF.Phase) +
                  NF.Amplitude * Cos (2.0 * Pi * NP * Time + NF.Phase)));
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (9);
            Freq : Long_Float := Year_Len / Periods (9);
            PF : Amp_Phase renames Constituents (14);
            PP : Long_Float := Year_Len / (2.0 * Periods (14));
            EF : Amp_Phase renames Constituents (15);  -- arbitrary
         begin --ampA*(ABS(SIN(freq*$A18+$E$13*ABS(SIN(freq*($A18+$E$14)))+phaseA))))
            A :=
              L.Amplitude *
              (Cos
                 (2.0 * Pi * Freq * Time + L.Phase +
                  PF.Amplitude *
                    abs
                    (Cos
                       (2.0 * Pi * PP * Time +
                        EF.Amplitude *
                          abs (Cos (2.0 * Pi * PP * Time + EF.Phase)) +
                        PF.Phase))));
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (7);
            Freq : Long_Float := Year_Len / Periods (7);
         begin --ampA*(ABS(SIN(freq*$A18+$E$13*ABS(SIN(freq*($A18+$E$14)))+phaseA))))
            T := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (12);
            Freq : Long_Float := Year_Len / Periods (12);
         begin --ampA*(ABS(SIN(freq*$A18+$E$13*ABS(SIN(freq*($A18+$E$14)))+phaseA))))
            FF := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (6);
            Freq : Long_Float := Year_Len / Periods (6);
         begin --ampA*(ABS(SIN(freq*$A18+$E$13*ABS(SIN(freq*($A18+$E$14)))+phaseA))))
            TD := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (8);
            Freq : Long_Float := Year_Len / Periods (8);
         begin --ampA*(ABS(SIN(freq*$A18+$E$13*ABS(SIN(freq*($A18+$E$14)))+phaseA))))
            DD := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (1);
            Freq : Long_Float := Year_Len / Periods (2);
         begin
            DPlain := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (10);
            Freq : Long_Float := Year_Len / Periods (11);
         begin
            N := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;
         declare
            use Ada.Numerics.Long_Elementary_Functions;
            L : Amp_Phase renames Constituents (13);
            Freq : Long_Float := Year_Len / Periods (22);
         begin
            F := L.Amplitude * Cos (2.0 * Pi * Freq * Time + L.Phase);
         end;

         declare
            A1 : Long_Float renames Constituents (16).Amplitude;
            A2 : Long_Float renames Constituents (17).Amplitude;
            A3 : Long_Float renames Constituents (18).Amplitude;
            A4 : Long_Float renames Constituents (19).Amplitude;
            D1 : Long_Float renames Constituents (20).Amplitude;
            D2 : Long_Float renames Constituents (21).Amplitude;
            D3 : Long_Float renames Constituents (22).Amplitude;
            D4 : Long_Float renames Constituents (23).Amplitude;
            DA2 : Long_Float renames Constituents (24).Amplitude;
            D2A2 : Long_Float renames Constituents (25).Amplitude;
            D2A : Long_Float renames Constituents (26).Amplitude;
            DA3 : Long_Float renames Constituents (27).Amplitude;
            D3A : Long_Float renames Constituents (28).Amplitude;
            DA : Long_Float renames Constituents (29).Amplitude;
         begin
            TF :=
              F + N + DPlain + FF + TD + DD + T + A1 * A + A2 * A**2 +
              A3 * A**3 + A4 * A**4 + D1 * D + D2 * D**2 + D3 * D**3 +
              D4 * D**4 + DA2 * D * (A**2) + D2A2 * (D**2) * (A**2) +
              D2A * (D**2) * A + DA3 * D * (A**3) + D3A * (D**3) * A +
              DA * D * A;
            Res (I) := (Time, TF);
         end;
      end loop;
      return Res;
   end Tide_Sum_Custom;

   function Tide_Sum
     (Template : in Data_Pairs; Constituents : in Long_Periods_Amp_Phase;
      Periods : in Long_Periods; Ref_Time : in Long_Float := 0.0;
      Scaling : in Long_Float := 1.0; Cos_Phase : in Boolean := True;
      Year_Len : in Long_Float := Year_Length; Integ : in Long_Float := 0.0) return Data_Pairs
   is
      Res : Data_Pairs := Template;
      pragma Unreferenced (Ref_Time);
   begin
      if Custom_Tide then
         Res :=
           Tide_Sum_Custom
             (Template, Constituents, Periods, 0.0, Scaling, Cos_Phase,
              Year_Len, Integ);
      else
         Res :=
           Tide_Sum_Diff
             (Template, Constituents, Periods, 0.0, Scaling, Cos_Phase,
              Year_Len, Integ);
      end if;
      return Res;
   end Tide_Sum;

   --  LTE: Laplace's Tidal Equation solver
   --
   --  Implements the solution to Laplace's Tidal Equation (LTE) which models
   --  atmospheric/oceanic standing wave responses to periodic forcing.
   --
   --  Mathematical background (see "Mathematical Geoenergy", Ch.12):
   --    The LTE is a 2nd-order PDE describing global-scale wave propagation
   --    on a rotating sphere (Earth). Solutions are wave modes modulated by
   --    the forcing function.
   --
   --  This implementation:
   --    1. Takes tidal forcing as input
   --    2. Applies wave number modulations (spatial modes)
   --    3. Computes nonlinear response including 2nd/3rd order terms
   --    4. Returns modulated time series
   --
   --  Parameters:
   --    Forcing - Input tidal forcing time series
   --    Wave_Numbers - Spatial wavenumber array (determines mode structure)
   --    Amp_Phase - Amplitude/phase for each mode
   --    Offset, K0, Trend, Accel - Baseline adjustments
   --    NonLin - Nonlinearity coefficient (1.0 = linear, >1 = nonlinear)
   --    Third - Third-order nonlinearity coefficient
   --
   function LTE
     (Forcing : in Data_Pairs; Wave_Numbers : in Modulations;
      Amp_Phase : in Modulations_Amp_Phase;
      Offset, K0, Trend, Accel : in Long_Float := 0.0;
      NonLin : in Long_Float := 1.0;
      Annual : in Annual_Harmonics := (0.0, 0.0, 0.0, 0.0);
      Third : in Long_Float := 0.0;
      Accel_Ref : in Long_Float := Long_Float'First)
      return Data_Pairs
   is
      Res : Data_Pairs := Forcing;
      Effective_Accel_Ref : constant Long_Float :=
        (if Accel_Ref = Long_Float'First then Res (Forcing'First).Date
         else Accel_Ref);
   begin
      for I in Forcing'Range loop
         declare
            LF : Long_Float := 0.0;
            use Ada.Numerics.Long_Elementary_Functions;
            Pi : Long_Float := Ada.Numerics.Pi;
         begin
            for J in Wave_Numbers'Range loop
               declare
                  M : GEM.LTE.Amp_Phase renames Amp_Phase (J);
                  SW : Long_Float;
               begin
                  SW :=
                    Sin
                      (2.0 * Pi * Wave_Numbers (J) * Res (I).Value + M.Phase) *
                    Exp (Res (I).Value * Third);
                  if SW < 0.0 then
                     SW := -(abs SW)**NonLin;
                  else
                     SW := SW**NonLin;
                  end if;
                  if Sinc > 0.0 then
                     LF :=
                       LF + M.Amplitude * SW / (abs (Res (I).Value) + Sinc);
                  else
                     LF := LF + M.Amplitude * SW;
                  end if;
               exception
                  when Constraint_Error =>
                     null;
               end;
            end loop;
            --  Integer exponent (2, not 2.0): Ada's "**" for a Long_Float
            --  base with a Long_Float exponent uses the general
            --  Exp(Y*Log(X)) formula, undefined (ARGUMENT_ERROR) for a
            --  negative base -- and (Date - Effective_Accel_Ref) IS
            --  negative for any date before the reference whenever that
            --  reference is supplied externally (Accel_Ref) rather than
            --  being this array's own Forcing'First (which by
            --  construction is always <= every date in the array, so
            --  the original "**2.0" here never actually hit this).
            --  "**2" (an Integer literal) instead resolves to the safe
            --  repeated-multiplication overload, correct for any sign.
            LF :=
              LF + Offset + K0 * Res (I).Value + Trend * Res (I).Date +
              Accel *
                (Res (I).Date -
                   Effective_Accel_Ref)**2; -- K0 is wavenumber=0 solution
            LF := LF + Annual.Ann1 * Sin(2.0 * Pi * Res (I).Date) 
                     + Annual.Ann2 * Cos(2.0 * Pi * Res (I).Date) 
                     + Annual.Semi1 * Sin(4.0 * Pi * Res (I).Date) 
                     + Annual.Semi2 * Cos(4.0 * Pi * Res (I).Date);
            Res (I).Value := LF;
         end;
      end loop;
      return Res;
   end LTE;

   -- some of the values may not be modifiesd so this is used to identify them
   function Is_Fixed (Value : in Long_Float) return Boolean is
      pragma Unreferenced (Value);
   begin
      return False;
   end Is_Fixed;

   --
   -- The following are utility functions for simulation
   --

   function Is_Valid (A, B, M : Long_Float) return Boolean is
   begin
      return not (A = 0.0 or else B = 0.0 or else M = 0.0);
   end Is_Valid;

   function Wrap_Pi (X : Long_Float) return Long_Float is
      use Ada.Numerics;
      Y : Long_Float := X;
      Two_Pi : constant Long_Float := 2.0 * Pi;
   begin
      while Y > Pi loop
         Y := Y - Two_Pi;
      end loop;
      while Y < -Pi loop
         Y := Y + Two_Pi;
      end loop;
      return Y;
   end Wrap_Pi;

   -- Bound a net increment into a phase-like angle in [-Pi/2, Pi/2]
   function Step_Angle (DV : Long_Float; Scale : Long_Float) return Long_Float is
      use Ada.Numerics.Long_Elementary_Functions;
      S : constant Long_Float := (if Scale <= 0.0 then 1.0 else Scale);
   begin
      return Arctan (DV / S);
   end Step_Angle;

   -- Robust local scale estimated from manifold increment magnitude
   function Local_Scale (DM : Long_Float) return Long_Float is
   begin
      if abs DM < 1.0E-12 then
         return 1.0;
      else
         return abs DM;
      end if;
   end Local_Scale;

   -- Detect a turning point in the manifold:
   -- slope changes sign between I-1 -> I and I -> I+1
   function Is_Turning_Point
     (Manifold : Data_Pairs;
      I        : Integer) return Boolean
   is
      D1, D2 : Long_Float;
   begin
      if I <= Manifold'First or else I >= Manifold'Last then
         return False;
      end if;

      if not Is_Valid
        (Manifold (I - 1).Value, Manifold (I).Value, Manifold (I + 1).Value)
      then
         return False;
      end if;

      D1 := Manifold (I).Value     - Manifold (I - 1).Value;
      D2 := Manifold (I + 1).Value - Manifold (I).Value;

      if abs D1 < 1.0E-12 or else abs D2 < 1.0E-12 then
         return False;
      end if;

      return (D1 > 0.0 and then D2 < 0.0) or else
             (D1 < 0.0 and then D2 > 0.0);
   end Is_Turning_Point;

   -- Main metric:
   -- high values indicate model/data follow the same manifold-defined step winding
   function Winding_Agreement
     (Model, Data, Manifold : in Data_Pairs) return Long_Float
   is
      use Ada.Numerics;
      use Ada.Numerics.Long_Elementary_Functions;

      Start : Integer := Model'First;
      Stop  : Integer := Model'Last;
      J     : Integer;

      Prev_Turn     : Integer := Integer'First;
      Turn_Count    : Integer := 0;

      Sum_MD2       : Long_Float := 0.0; -- model vs data angle mismatch
      Sum_MM2       : Long_Float := 0.0; -- model vs manifold angle mismatch
      Sum_DM2       : Long_Float := 0.0; -- data vs manifold angle mismatch
      Sum_Sign      : Long_Float := 0.0; -- sign coherence bonus
      Sum_Weight    : Long_Float := 0.0;

      Sigma_Angle   : constant Long_Float := Pi / 4.0;

      function Min3 (A, B, C : Integer) return Integer is
      begin
         if A <= B and then A <= C then
            return A;
         elsif B <= A and then B <= C then
            return B;
         else
            return C;
         end if;
      end Min3;

   begin
      -- Basic shared bounds
      Start := Min3 (Model'First, Data'First, Manifold'First);
      Stop  := Min3 (Model'Last,  Data'Last,  Manifold'Last);

      -- Trim leading invalids
      while Start <= Stop loop
         exit when Is_Valid
           (Model (Start).Value, Data (Start).Value, Manifold (Start).Value);
         Start := Start + 1;
      end loop;

      -- Trim trailing invalids
      while Stop >= Start loop
         exit when Is_Valid
           (Model (Stop).Value, Data (Stop).Value, Manifold (Stop).Value);
         Stop := Stop - 1;
      end loop;

      if Stop - Start < 4 then
         return 0.0;
      end if;

      J := Start;

      -- Walk turning points of manifold; each interval between turns is one hidden step
      for I in Start + 1 .. Stop - 1 loop
         if Is_Valid (Model (I - 1).Value, Data (I - 1).Value, Manifold (I - 1).Value) and then
               Is_Valid (Model (I).Value,     Data (I).Value,     Manifold (I).Value) and then
               Is_Valid (Model (I + 1).Value, Data (I + 1).Value, Manifold (I + 1).Value) and then
               Is_Turning_Point (Manifold, I)
         then
            if Prev_Turn = Integer'First then
               Prev_Turn := I;
            else
               declare
                  S : constant Integer := Prev_Turn;
                  E : constant Integer := I;

                  DMf : constant Long_Float := Manifold (E).Value - Manifold (S).Value;
                  DX  : constant Long_Float := Model (E).Value    - Model (S).Value;
                  DY  : constant Long_Float := Data (E).Value     - Data (S).Value;

                  Scale : constant Long_Float := Local_Scale (DMf);

                  Phi_M : constant Long_Float := Step_Angle (DMf, Scale);
                  Phi_X : constant Long_Float := Step_Angle (DX,  Scale);
                  Phi_Y : constant Long_Float := Step_Angle (DY,  Scale);

                  D_XY  : constant Long_Float := Wrap_Pi (Phi_X - Phi_Y);
                  D_XM  : constant Long_Float := Wrap_Pi (Phi_X - Phi_M);
                  D_YM  : constant Long_Float := Wrap_Pi (Phi_Y - Phi_M);

                  Weight : constant Long_Float := abs DMf;

                  Sign_XM : constant Long_Float :=
                    (if DX * DMf > 0.0 then 1.0 else 0.0);
                  Sign_YM : constant Long_Float :=
                    (if DY * DMf > 0.0 then 1.0 else 0.0);
                  Sign_XY : constant Long_Float :=
                    (if DX * DY > 0.0 then 1.0 else 0.0);

                  Sign_Score : constant Long_Float :=
                    (Sign_XM + Sign_YM + Sign_XY) / 3.0;
               begin
                  if E > S and then Weight > 0.0 then
                     Sum_MD2    := Sum_MD2 + Weight * D_XY * D_XY;
                     Sum_MM2    := Sum_MM2 + Weight * D_XM * D_XM;
                     Sum_DM2    := Sum_DM2 + Weight * D_YM * D_YM;
                     Sum_Sign   := Sum_Sign + Weight * Sign_Score;
                     Sum_Weight := Sum_Weight + Weight;
                     Turn_Count := Turn_Count + 1;
                  end if;
               end;

               Prev_Turn := I;
            end if;
         end if;
      end loop;

      if Turn_Count < 2 or else Sum_Weight <= 0.0 then
         return 0.0;
      end if;

      declare
         E_XY   : constant Long_Float := Sum_MD2 / Sum_Weight;
         E_XM   : constant Long_Float := Sum_MM2 / Sum_Weight;
         E_YM   : constant Long_Float := Sum_DM2 / Sum_Weight;
         Sgn    : constant Long_Float := Sum_Sign / Sum_Weight;

         -- exponential penalty for angular mismatch
         Q_XY   : constant Long_Float := Exp (-E_XY / (2.0 * Sigma_Angle * Sigma_Angle));
         Q_XM   : constant Long_Float := Exp (-E_XM / (2.0 * Sigma_Angle * Sigma_Angle));
         Q_YM   : constant Long_Float := Exp (-E_YM / (2.0 * Sigma_Angle * Sigma_Angle));

         -- combine direct agreement + manifold coherence + sign coherence
         Score  : Long_Float :=
           0.45 * Q_XY +
           0.25 * Q_XM +
           0.20 * Q_YM +
           0.10 * Sgn;
      begin
         if Score < 0.0 then
            return 0.0;
         elsif Score > 1.0 then
            return 1.0;
         else
            return Score;
         end if;
      end;

   exception
      when Constraint_Error =>
         return 0.0;
   end Winding_Agreement;


   -- Pearson's Correlation Coefficient
   function CC (X, Y : in Data_Pairs) return Long_Float is
      N : Integer := 0; -- X'Length;
      sum_X, sum_Y, sum_XY, squareSum_X, squareSum_Y : Long_Float := 0.0;
      use Ada.Numerics.Long_Elementary_Functions;
      J : Integer; --:= Y'First;
      Denominator : Long_Float;
      Start, Stop : Integer;
   begin
      Start := X'First;
      Stop := X'Last;
      for I in X'Range loop
         if X (I).Value = 0.0 or Y (I).Value = 0.0 then
            Start := I + 1;
         else
            exit;
         end if;
      end loop;
      for I in reverse X'Range loop
         if X (I).Value = 0.0 or Y (I).Value = 0.0 then
            Stop := I - 1;
         else
            exit;
         end if;
      end loop;
      J := Y'First + (Start - X'First);
      for I in Start .. Stop loop
         -- sum of elements of array X.
         if X (I).Date > Start_Year and X (I).Date < End_Year and
           not (X (I).Value = 0.0 or Y (J).Value = 0.0)
         then

            sum_X := sum_X + X (I).Value;

            -- sum of elements of array Y.
            sum_Y := sum_Y + Y (J).Value;

            -- sum of X[i] * Y[i].
            sum_XY := sum_XY + X (I).Value * Y (J).Value;

            -- sum of square of array elements.
            squareSum_X := squareSum_X + X (I).Value * X (I).Value;
            squareSum_Y := squareSum_Y + Y (J).Value * Y (J).Value;
            N := N + 1;
         end if;

         J := J + 1;
      end loop;
      -- assert the denominator that it doesn't hit zero
      Denominator :=
        (Long_Float (N) * squareSum_X - sum_X * sum_X) *
        (Long_Float (N) * squareSum_Y - sum_Y * sum_Y);
      if Denominator <= 0.0 then
         return 0.0;
      else
         return (Long_Float (N) * sum_XY - sum_X * sum_Y) / Sqrt (Denominator);
      end if;
   exception
      when Constraint_Error =>
         return 0.0;
   end CC;

   -- A zero-crossing metric that is faster than CC which can be used to
   -- improve fits to time-series with many zero crossings, as the precise
   -- amplitude is not critical
   function Xing (X, Y : in Data_Pairs) return Long_Float is
      sum_absXY, sum_XY : Long_Float := 0.0;
      J : Integer := Y'First;
   begin
      for I in X'Range loop
         -- sum of X[i] * Y[i].
         sum_XY := sum_XY + X (I).Value * Y (J).Value;
         sum_absXY := sum_absXY + abs (X (J).Value) * abs (Y (J).Value);

         J := J + 1;
      end loop;
      return sum_XY / sum_absXY;
   end Xing;

   function Diff (Raw : in Data_Pairs) return Data_Pairs is
      Res : Data_Pairs := Raw;
   begin
      Res (Raw'First).Value := 0.0;
      for I in Raw'First + 1 .. Raw'Last loop
         Res (I).Value := Raw (I).Value - Raw (I - 1).Value;
      end loop;
      return Res;
   end Diff;

   function DER_CC (Model, Data : in Data_Pairs) return Long_Float is
   begin
      return CC (X => Diff (Model), Y => Diff (Data));
   end DER_CC;

   function RMS
     (X, Y : in Data_Pairs; Ref, Offset : in Long_Float) return Long_Float
   is
      sum_XY : Long_Float := 0.0;
      use Ada.Numerics.Long_Elementary_Functions;
      J : Integer := Y'First;
   begin
      for I in X'Range loop
         -- sum of (X[i] - Y[i])^2
         sum_XY := sum_XY + (X (I).Value - Y (J).Value + Offset)**2;
         J := J + 1;
      end loop;
      return 1.0 / (1.0 + Sqrt (sum_XY) / Ref);
   end RMS;

   function CID (X, Y : in Data_Pairs) return Long_Float is
      -- sum_XY,
      sum_X, sum_Y : Long_Float := 0.0;
      use Ada.Numerics.Long_Elementary_Functions;
   begin
      -- sum_XY := CC(X,Y);
      for I in X'First .. X'Last - 1 loop
         sum_X := sum_X + (X (I).Value - X (I + 1).Value)**2;
      end loop;
      for I in Y'First .. Y'Last - 1 loop
         sum_Y := sum_Y + (Y (I).Value - Y (I + 1).Value)**2;
      end loop;

      -- return sum_XY * Sqrt(Long_Float'Min(Sum_X, Sum_Y) / Long_Float'Max(Sum_X, Sum_Y));
      return
        Sqrt (Long_Float'Min (sum_X, sum_Y) / Long_Float'Max (sum_X, sum_Y));
   end CID;

   function DTW_Distance
     (X, Y : in Data_Pairs; Window_Size : Positive) return Long_Float
   is
      function Distance
        (X, Y : in Data_Pairs; Window_Size : Positive) return Long_Float
      is
         N : Positive := X'Length;
         type Real_Array is
           array (X'First - Window_Size .. X'Last + Window_Size) of Long_Float;
         DTW_Current, DTW_Previous : Real_Array := (others => Long_Float'Last);
      begin
         DTW_Previous (X'First) := 0.0;

         for I in X'First .. X'Last loop
            DTW_Current (X'First) := Long_Float'Last; -- Reset current row

            for J in
              Integer'Max (X'First, I - Window_Size) ..
                Integer'Min (X'Last, I + Window_Size)
            loop
               declare
                  Cost : Long_Float := abs (X (I).Value - Y (J).Value);
                  Min_Cost : Long_Float;
                  J_Previous : Integer := Integer'Max (J - 1, Y'First);
               begin
                  if Y (J).Value = 0.0 then
                     Cost := 0.0;
                  end if;
                  -- Compute minimum cost considering the DTW constraint
                  Min_Cost :=
                    Long_Float'Min
                      (Long_Float'Min
                         (DTW_Previous (J_Previous), DTW_Current (J_Previous)),
                       (if J > Y'First then DTW_Previous (J)
                        else Long_Float'Last));

                  DTW_Current (J) := Cost + Min_Cost;
               end;
               N := J;
            end loop;

            -- Swap the rows
            DTW_Previous := DTW_Current;
         end loop;

         return DTW_Current (N);
      end Distance;

      function Neg (X : in Data_Pairs) return Data_Pairs is
         N : Data_Pairs := X;
      begin
         for I in N'Range loop
            N (I).Value := -N (I).Value;
         end loop;
         return N;
      end Neg;
      Max : Long_Float;
   begin
      Max := Distance (Neg (Y), Y, Window_Size);
      return (Max - Distance (X, Y, Window_Size)) / Max;
   end DTW_Distance;

   procedure Sort (Arr : in out Data_Pairs) is
      Temp : Long_Float;
      Swapped : Boolean;
   begin
      for I in Arr'First .. Arr'Last - 1 loop
         Swapped := False;
         for J in Arr'First .. Arr'Last - I loop
            if Arr (J).Value > Arr (J + 1).Value then
               Temp := Arr (J).Value;
               Arr (J).Value := Arr (J + 1).Value;
               Arr (J + 1).Value := Temp;
               Swapped := True;
            end if;
         end loop;
         exit when not Swapped;
      end loop;
   end Sort;

   function EMD
     (X, Y : in Data_Pairs; Derivative : in Boolean := False) return Long_Float
   is

      function Wasserstein1 (Y1, Y2 : Data_Pairs) return Long_Float is
         Sorted_Y1 : Data_Pairs := Y1;
         Sorted_Y2 : Data_Pairs := Y2;
         Distance : Long_Float := 0.0;
      begin
         -- Sort both arrays
         Sort (Sorted_Y1);
         Sort (Sorted_Y2);

         -- Compute W1 distance (for equal-length arrays)
         for I in Y1'Range loop
            Distance :=
              Distance + abs (Sorted_Y1 (I).Value - Sorted_Y2 (I).Value);
         end loop;

         return Distance / Long_Float (Y1'Length);
      end Wasserstein1;

   begin
      if Derivative then
         return 1.0 / (1.0 + Wasserstein1 (Diff (X), Diff (Y)));
      else
         return 1.0 / (1.0 + Wasserstein1 (X, Y));
      end if;
   end EMD;

   function Median (Arr : in Data_Pairs) return Long_Float is
      Temp : Data_Pairs := Arr;
   begin
      Sort (Temp);
      return Temp ((Arr'Last - Arr'First) / 2 + Arr'First).Value;
   end Median;

   function Mean
     (Arr : in Data_Pairs; Absolute : in Boolean := False) return Long_Float
   is
      Temp : Long_Float := 0.0;
   begin
      for I in Arr'Range loop
         if Absolute then
            Temp := Temp + abs Arr (I).Value;
         else
            Temp := Temp + Arr (I).Value;
         end if;
      end loop;
      return Temp / Long_Float (Arr'Length);
   end Mean;

   function Scaled_Error_Metric (X, Y : in Data_Pairs) return Long_Float is

      -- Function to compute the Median Absolute Deviation (MAD)
      function Compute_MAD (Y : Data_Pairs) return Long_Float is
         Median_Y : constant Long_Float := Median (Y);
         Deviations : Data_Pairs := Y;
      begin
         for I in Y'Range loop
            Deviations (I).Value := abs (Y (I).Value - Median_Y);
         end loop;
         return Median (Deviations);
      end Compute_MAD;

      -- Function to compute the global-scale
      function Compute_Global_Scale
        (Y : Data_Pairs; Alpha : Long_Float := 0.01;
         Epsilon : Long_Float := 1.0e-10) return Long_Float
      is
         MAD_Y : constant Long_Float := Compute_MAD (Y);
         Mean_Abs_Y : constant Long_Float := Mean (Y, True);
         Scaled_Mean : constant Long_Float :=
           Alpha * (if Mean_Abs_Y > Epsilon then Mean_Abs_Y else Epsilon);
      begin
         return Long_Float'Max (MAD_Y, Scaled_Mean);
      end Compute_Global_Scale;

      -- Function to compute the Score
      function Compute_Score
        (X, Y : Data_Pairs; Alpha : Long_Float := 0.01) return Long_Float
      is
         Global_Scale : constant Long_Float := Compute_Global_Scale (Y, Alpha);
         Sum : Long_Float := 0.0;
      begin
         for I in Y'Range loop
            declare
               Error : constant Long_Float := X (I).Value - Y (I).Value;
               Scale : constant Long_Float :=
                 Long_Float'Max (Alpha * abs (Y (I).Value), Global_Scale);
            begin
               Sum := Sum + (Error**2) / (Scale**2);
            end;
         end loop;
         return Sum / Long_Float (Y'Length);
      end Compute_Score;

      Alpha : constant Long_Float := 0.01;
   begin
      return 1.0 / (1.0 + Compute_Score (X, Y, Alpha));
   end Scaled_Error_Metric;

   Pi : constant Long_Float := Ada.Numerics.Pi;
   Mult : constant Long_Float := GEM.Getenv ("FMULT", 1.008);
   Step : constant Long_Float := GEM.Getenv ("FSTEP", 0.18);
   F_Start : constant Long_Float := GEM.Getenv ("FSTART", 0.01);
   F_End : constant Long_Float := 1_000.0;

   function Min_Entropy_RMS (X, Y : in Data_Pairs) return Long_Float is
      use Ada.Numerics.Long_Elementary_Functions;
      Value, Sum : Long_Float := 0.0;
      S, C : Long_Float; -- cumulative
      F : Long_Float := F_Start;
      N : Integer := 1;
   begin
      loop
         S := 0.0;
         C := 0.0;
         for I in X'Range loop
            S := S + Sin (2.0 * Pi * F * X (I).Value) * Y (I).Value;
            C := C + Cos (2.0 * Pi * F * X (I).Value) * Y (I).Value;
         end loop;
         Value := Value + (S * S + C * C);
         Sum := Sum + Sqrt ((S * S + C * C));
         if Linear_Step then
            F := F + Step;
         else
            F := F * Mult;
         end if;
         exit when F > F_End;
         N := N + 1;
      end loop;
      Sum := Sum * Sum / Long_Float (N * N);
      Value := (Value - Sum) / Sum;
      --Text_IO.Put_Line(Value'Img & Sum'Img);
      return Value;
   end Min_Entropy_RMS;

   procedure ME_Power_Spectrum
     (Forcing, Model, Data : in Data_Pairs;
      Model_Spectrum, Data_Spectrum : out Data_Pairs; RMS : out Long_Float;
      Phase : in Boolean := False)
   is
      use Ada.Numerics.Long_Elementary_Functions;
      Model_S : Data_Pairs (Model'Range);
      Data_S : Data_Pairs (Data'Range);
      Value, Sum : Long_Float := 0.0;
      F : Long_Float := F_Start;
      S, C : Long_Float; -- cumulative
      N : Integer := 1;
   begin
      for J in Data'Range loop
         S := 0.0;
         C := 0.0;
         for I in Data'First + 8 .. Data'Last loop  -- remove Init value
            S := S + Sin (2.0 * Pi * F * Forcing (I).Value) * Data (I).Value;
            C := C + Cos (2.0 * Pi * F * Forcing (I).Value) * Data (I).Value;
         end loop;
         Data_S (J).Date := F;
         if Phase then
            Data_S (J).Value := S;
         else
            Data_S (J).Value := (S * S + C * C);
         end if;
         Value := Value + Data_S (J).Value;
         Sum := Sum + Sqrt (Data_S (J).Value);
         if Linear_Step then
            F := F + Step;
         else
            F := F * Mult;
         end if;
      end loop;
      F := F_Start;
      for J in Model'Range loop
         S := 0.0;
         C := 0.0;
         for I in Model'First + 8 .. Model'Last loop  -- remove Init value
            S := S + Sin (2.0 * Pi * F * Forcing (I).Value) * Model (I).Value;
            C := C + Cos (2.0 * Pi * F * Forcing (I).Value) * Model (I).Value;
         end loop;
         Model_S (J).Date := F;
         if Phase then
            Model_S (J).Value := S;
         else
            Model_S (J).Value := (S * S + C * C);
         end if;
         Value := Value + Model_S (J).Value;
         Sum := Sum + Sqrt (Model_S (J).Value);
         if Linear_Step then
            F := F + Step;
         else
            F := F * Mult;
         end if;
      end loop;
      Model_Spectrum := Model_S;
      Data_Spectrum := Data_S;
      Sum := Sum * Sum / Long_Float (N * N);
      RMS := (Value - Sum) / Sum;
   end ME_Power_Spectrum;

   function Min_Entropy_Power_Spectrum (X, Y : in Data_Pairs) return Long_Float
   is
      First : Positive := X'First;
      Last : Positive := X'Last;
      Mid : Positive := (First + Last) / 2;
      FD : Data_Pairs := Y (First .. Mid);
      LD : Data_Pairs := Y (Mid .. Last);
      RMS : Long_Float;
   begin
      if GEM.Getenv ("MERMS", False) then
         return Min_Entropy_RMS (X, Y);
      else
         ME_Power_Spectrum (X, FD, LD, FD, LD, RMS, False);
         return CC (Filter9Point (FD), Filter9Point (LD));
      end if;
   end Min_Entropy_Power_Spectrum;

   function FT_CC (Model, Data, Forcing : in Data_Pairs) return Long_Float is
      Model_S : Data_Pairs := Model;
      Data_S : Data_Pairs := Data;
      T : Data_Pairs := Forcing;
      RMS : Long_Float;
   begin
      for I in T'Range loop
         T (I).Value := T (I).Date;
      end loop;
      ME_Power_Spectrum
        (Forcing => T, Model => Model, Data => Data, Model_Spectrum => Model_S,
         Data_Spectrum => Data_S, RMS => RMS);
      Model_S := Window (Model_S, 2);
      Data_S := Window (Data_S, 2);
      -- return CC(Model_S, Data_S);
      return DTW_Distance (Model_S, Data_S, 9);
   end FT_CC;

   -- Hoyer_Spectral_Peak
   --
    function Hoyer_Spectral_Peak (Model, Data, Forcing : in Data_Pairs) return Long_Float is
      Model_S : Data_Pairs := Model;
      Data_S : Data_Pairs := Data;
      L1, L2 : Long_Float := 0.0;
      Len : Long_Float;
      RMS : Long_Float;
      Num, Den : Long_Float;
      use Ada.Numerics.Long_Elementary_Functions;
   begin
      ME_Power_Spectrum
        (Forcing => Forcing, Model => Model, Data => Data, Model_Spectrum => Model_S,
         Data_Spectrum => Data_S, RMS => RMS);
      Len := Long_Float(Data_S'Length);
      for I in Data_S'First+1 ..  Data_S'Last loop
         L1 := L1 + Data_S(I).Value;
         L2 := L2 + Data_S(I).Value * Data_S(I).Value;
      end loop;
      L2 := Sqrt(L2);
      Num := Sqrt(Len) - L1/L2;
      Den := Sqrt(Len) - 1.0;
      return Num/Den;
   end Hoyer_Spectral_Peak;  
   
   procedure Dump (Model, Data : in Data_Pairs; Run_Time : Long_Float := 200.0)
   is
   begin
      for I in Data'Range loop
         Text_IO.Put_Line
           (Data (I).Date'Img & " " & Model (I).Value'Img & " " &
            Data (I).Value'Img);
         exit when Model (I).Date > Run_Time;
      end loop;
   end Dump;

   Running : Boolean := True;

   procedure Stop is
   begin
      Running := False;
   end Stop;

   function Halted return Boolean is
   begin
      return not Running;
   end Halted;

   procedure Continue is
   begin
      Running := True;
   end Continue;

   --
   -- protect the file from reentrancy
   --

   protected Safe is
      procedure Save
        (Model, Data, Forcing : in Data_Pairs; File_Name : in String;
         IR : in Long_Float := 0.0);
      procedure Save
        (Model : in Data_Pairs; Mag : in Integer; File_Name : in String);
   end Safe;

   protected body Safe is
      procedure Save
        (Model, Data, Forcing : in Data_Pairs; File_Name : in String;
         IR : in Long_Float := 0.0)
      is
         FT : Text_IO.File_Type;
         Model_S : Data_Pairs := Model;
         Data_S : Data_Pairs := Data;
         RMS : Long_Float;
         --  Modulation-only power spectrum, gated by UNCOMPENSATED (default
         --  False -- old behavior unchanged). The saved Model already has
         --  the post-LTE lag-12 IR differential applied
         --  (Model(I) := Model(I) - IR*Model(I-12), computed with both taps
         --  reading the pre-adjustment Model); the regression that produced
         --  the pre-adjustment modulation was fit against DR = Data +
         --  IR*Data(I-12), not Data itself. To see the modulation alone,
         --  against what it was actually fit to match: undo the lag-12
         --  differential on Model (a recursive inverse, built up in
         --  increasing-index order using the already-recovered value 12
         --  steps back), reproduce DR on Data (non-recursive -- both taps
         --  are the original Data, exactly like the forward construction),
         --  then rescale the decompensated Model by a single best-fit
         --  scalar so its amplitude is comparable to DR's -- the raw
         --  modulation has no inherent reason to already share that scale.
         --  This only feeds the spectrum below; the CSV row output further
         --  down still uses the untouched Model/Data/Forcing parameters, so
         --  the actual fit (IR included) is never altered.
         Uncompensated_Mode : constant Boolean :=
           GEM.Getenv ("UNCOMPENSATED", False);
         Model_Spec : Data_Pairs := Model;
         Data_Spec : Data_Pairs := Data;
      begin
         --  Unrolled, the Model_Spec recursion below is a geometric series
         --  in IR (Model_orig(I) = sum_k IR**k * Model_final(I-12k)) and
         --  only stays bounded for |IR| < 1, regardless of sign -- for
         --  |IR| >= 1 (seen in practice, e.g. tpi's IR=-1.21) it diverges
         --  across the record, which then wrecks the Scale computation
         --  below (dominated by the blown-up tail). Guarded: skip the
         --  decompensation and fall through to the unmodified Model/Data
         --  (same as Uncompensated_Mode=False) rather than emitting
         --  garbage into the saved spectrum.
         if Uncompensated_Mode and then IR /= 0.0 and then abs (IR) >= 1.0
         then
            Text_IO.Put_Line
              ("WARNING: |IR| =" & IR'Img &
               " >= 1 -- decompensation is unstable at this magnitude." &
               " Skipping for the saved spectrum.");
         end if;
         if Uncompensated_Mode and then IR /= 0.0 and then abs (IR) < 1.0 then
            for I in Model'First + 12 .. Model'Last loop
               Model_Spec (I).Value :=
                 Model (I).Value + IR * Model_Spec (I - 12).Value;
            end loop;
            for I in Data'First + 12 .. Data'Last loop
               Data_Spec (I).Value :=
                 Data (I).Value + IR * Data (I - 12).Value;
            end loop;
            declare
               Num, Den : Long_Float := 0.0;
               Scale : Long_Float := 1.0;
            begin
               for I in Model_Spec'Range loop
                  Num := Num + Model_Spec (I).Value * Data_Spec (I).Value;
                  Den := Den + Model_Spec (I).Value * Model_Spec (I).Value;
               end loop;
               if Den > 0.0 then
                  Scale := Num / Den;
               end if;
               for I in Model_Spec'Range loop
                  Model_Spec (I).Value := Model_Spec (I).Value * Scale;
               end loop;
            end;
         end if;
         Text_IO.Create
           (File => FT, Name => File_Name, Mode => Text_IO.Out_File);
         if Model_S'Length < 10_000 then
            if Is_Minimum_Entropy then
               ME_Power_Spectrum
                 (Forcing => Model_Spec, Model => Forcing, Data => Data_Spec,
                  Model_Spectrum => Model_S, Data_Spectrum => Data_S,
                  RMS => RMS, Phase => False);
            else
               ME_Power_Spectrum
                 (Forcing => Forcing, Model => Model_Spec, Data => Data_Spec,
                  Model_Spectrum => Model_S, Data_Spectrum => Data_S,
                  RMS => RMS);
               Model_S := Window (Model_S, 2);
               Data_S := Window (Data_S, 2);
            end if;
         end if;
         for I in Data'Range loop
            Text_IO.Put_Line
              (FT,
               Data (I).Date'Img & ", " & Model (I).Value'Img & ", " &
               Data (I).Value'Img & ", " & Forcing (I).Value'Img & ", " &
               Data_S (I).Date'Img & ", " & Model_S (I).Value'Img & ", " &
               Data_S (I).Value'Img);
         end loop;
         Text_IO.Close (FT);
      end Save;

      procedure Save
        (Model : in Data_Pairs; Mag : in Integer; File_Name : in String)
      is
         FT : Text_IO.File_Type;
         Model_S : Data_Pairs := Expand (Model, Mag);
      begin
         if Mag > 1 then
            Text_IO.Create
              (File => FT, Name => File_Name, Mode => Text_IO.Out_File);
            for I in Model_S'Range loop
               Text_IO.Put_Line
                 (FT, Model_S (I).Date'Img & ", " & Model_S (I).Value'Img);
            end loop;
            Text_IO.Close (FT);
         end if;
      end Save;
   end Safe;

   procedure Save
     (Model, Data, Forcing : in Data_Pairs;
      File_Name : in String := "lte_results.csv";
      IR : in Long_Float := 0.0)
   is
   begin
      Safe.Save (Model, Data, Forcing, File_Name, IR);
      Safe.Save (Forcing, Magnify, "mag_" & File_Name);
   end Save;

   -- 3 point median
   function Median (Raw : in Data_Pairs) return Data_Pairs is
      Res : Data_Pairs := Raw;
   begin
      for I in Raw'First + 1 .. Raw'Last - 1 loop
         if Raw (I - 1).Value < Raw (I).Value then
            if Raw (I - 1).Value >= Raw (I + 1).Value then
               Res (I).Value := Raw (I - 1).Value;
            elsif Raw (I).Value < Raw (I + 1).Value then
               Res (I).Value := Raw (I).Value;
            else
               Res (I).Value := Raw (I + 1).Value;
            end if;
         else
            if Raw (I - 1).Value < Raw (I + 1).Value then
               Res (I).Value := Raw (I - 1).Value;
            elsif Raw (I).Value >= Raw (I + 1).Value then
               Res (I).Value := Raw (I).Value;
            else
               Res (I).Value := Raw (I + 1).Value;
            end if;
         end if;
      end loop;
      return Res;
   end Median;

   function Window
     (Raw : in Data_Pairs; Lobe_Width : in Positive) return Data_Pairs
   is
      Res : Data_Pairs := Raw;
   begin
      for I in Raw'First + Lobe_Width .. Raw'Last - Lobe_Width loop
         Res (I).Value := 0.0;
         for J in I - Lobe_Width .. I + Lobe_Width loop
            Res (I).Value := Res (I).Value + Raw (J).Value;
         end loop;
         Res (I).Value := Res (I).Value / Long_Float (2 * Lobe_Width + 1);
      end loop;
      return Res;
   end Window;

   -- Multiple Linear Regression types

   package MLR is new Ada.Numerics.Generic_Real_Arrays (Real => Long_Float);
   subtype Vector is MLR.Real_Vector;
   subtype Matrix is MLR.Real_Matrix;

   function To_Matrix
     (Source : Vector; Column_Vector : Boolean := True) return Matrix
   is
      Result : Matrix (1 .. 1, Source'Range);
   begin
      for Column in Source'Range loop
         Result (1, Column) := Source (Column);
      end loop;
      if Column_Vector then
         return MLR.Transpose (Result);
      else
         return Result;
      end if;
   end To_Matrix;

   function To_Row_Vector
     (Source : Matrix; Column : Positive := 1) return Vector
   is
      Result : Vector (Source'Range (1));
   begin
      for Row in Result'Range loop
         Result (Row) := Source (Row, Column);
      end loop;
      return Result;
   end To_Row_Vector;

   function Regression_Coefficients
     (Source : Vector; Regressors : Matrix) return Vector
   is
      Result : Matrix (Regressors'Range (2), 1 .. 1);
      Nil : Vector (1 .. 0);
   begin
      if Source'Length /= Regressors'Length (1) then
         raise Constraint_Error;
      end if;
      declare
         Regressors_T : constant Matrix := MLR.Transpose (Regressors);
         use MLR;
      begin
         Result :=
           MLR.Inverse
             (Regressors_T * Regressors +
              Ridge_Lambda *
                MLR.Unit_Matrix
                  (Order   => Regressors'Length (2),
                   First_1 => Regressors'First (2),
                   First_2 => Regressors'First (2))) *
           Regressors_T * To_Matrix (Source);
      end;
      return To_Row_Vector (Source => Result);
   exception
      when Constraint_Error => -- Singular
         Text_IO.Put_Line ("Singular result, converging?");
         delay 1.0;
         return Nil; -- Source;  -- Doesn't matter, will give a junk result
   end Regression_Coefficients;

   procedure Regression_Factors
     (Data_Records : in Data_Pairs;
      --First, Last,  -- Training Interval

      NM : in Positive; -- # modulations
      Forcing : in Data_Pairs;  -- Value @ Time
      -- Factors_Matrix : in out Matrix;

      DBLT : in Periods;
      DALTAP : out Amp_Phases;
      DALEVEL : out Long_Float;
      DAK0 : out Long_Float;
      Secular_Trend : in out Long_Float;
      Accel : out Long_Float;
      Singular : out Boolean;
      Annual : out Annual_Harmonics;
      Third : in Long_Float := 0.0;
      IR : in Long_Float := 0.0)
   is

      use Ada.Numerics.Long_Elementary_Functions;
      First : Integer := Data_Records'First;
      Last : Integer := Data_Records'Last;
      Pi : Long_Float := Ada.Numerics.Pi;
      Trend : Boolean := Secular_Trend > 0.0;
      -- Add_Trend : Integer := Integer(Secular_Trend);
      Add_Trend : Integer := 6 * Boolean'Pos (Trend);
      Num_Coefficients : constant Integer :=
        2 + NM * 2 + Add_Trend; -- 4 for annual harmonics
      RData : Vector (1 .. Last - First + 1);
      Factors_Matrix : Matrix (1 .. Last - First + 1, 1 .. Num_Coefficients);
      Value : Long_Float;
      --  Fold the same post-LTE lag-12 delay differential
      --  (Model(I) := Model(I) - IR*Model(I-12), see the Save procedure
      --  below and Calc_Forcing in -solution.adb) into the regression
      --  BASIS instead of approximating it via a pre-emphasized target
      --  (DR = Data + IR*Data(I-12)). Fitting X against DR then applying
      --  L post-hoc is NOT equivalent to directly minimizing
      --  ||L*X*beta - Data||^2 in general (L is not orthogonal), so it's
      --  provably non-optimal for the actual quantity the search's own
      --  accept/reject metric measures (Model_final vs Data). Regressing
      --  L*X directly against the untransformed Data_Records (the caller
      --  skips the DR pre-emphasis in this mode, passing raw Data through
      --  unchanged) is a single, jointly-optimal regression -- the
      --  standard pre-whitening/GLS way of handling a known
      --  autocorrelation structure, rather than approximating around it.
      --  Gated by UNCOMPENSATED (default False, old behavior byte-for-byte
      --  unchanged) so the original DR-based approach stays available.
      Uncompensated_Mode : constant Boolean :=
        GEM.Getenv ("UNCOMPENSATED", False);
   begin
      Annual := (0.0, 0.0, 0.0, 0.0);
      for I in First .. Last loop
         RData (I - First + 1) := Data_Records (I).Value;
         Factors_Matrix (I - First + 1, 1) := 1.0;  -- DC offset
         Factors_Matrix (I - First + 1, 2) := Forcing (I).Value;
         for K in DBLT'First .. NM loop  -- D.B.LT'First = 1
            Value :=
              Sin (2.0 * Pi * DBLT (K) * Forcing (I).Value) *
              Exp (Forcing (I).Value * Third);
            Factors_Matrix (I - First + 1, 3 + (K - 1) * 2) := Value;
            Value :=
              Cos (2.0 * Pi * DBLT (K) * Forcing (I).Value) *
              Exp (Forcing (I).Value * Third);
            Factors_Matrix (I - First + 1, 4 + (K - 1) * 2) := Value;
         end loop;
         if Trend then
            Factors_Matrix (I - First + 1, Num_Coefficients - 2) := Sin(4.0 * Pi * Forcing (I).Date);
            Factors_Matrix (I - First + 1, Num_Coefficients - 3) := Cos(4.0 * Pi * Forcing (I).Date);
            Factors_Matrix (I - First + 1, Num_Coefficients - 4) := Sin(2.0 * Pi * Forcing (I).Date);
            Factors_Matrix (I - First + 1, Num_Coefficients - 5) := Cos(2.0 * Pi * Forcing (I).Date);
            Factors_Matrix (I - First + 1, Num_Coefficients - 1) :=
              Forcing (I).Date;
            --  Integer exponent -- see the matching comment in LTE; base
            --  is never negative here (I ranges over First..Last of the
            --  same array) but kept consistent with the same safe form.
            Factors_Matrix (I - First + 1, Num_Coefficients) :=
              (Forcing (I).Date - Forcing (First).Date)**2;
         end if;
      end loop;

      --  Fold L (the lag-12 delay differential) into every basis column,
      --  matching exactly how it's later applied to the combined LTE
      --  output -- both taps read the not-yet-modified column, via the
      --  same reverse-order trick used on Model itself, so this is an
      --  exact (not approximate) transform of the basis, unlike DR's
      --  single-term approximation of L^{-1} on the target side.
      if Uncompensated_Mode and then IR /= 0.0 then
         for J in 1 .. Num_Coefficients loop
            for Row in reverse 13 .. Last - First + 1 loop
               Factors_Matrix (Row, J) :=
                 Factors_Matrix (Row, J) - IR * Factors_Matrix (Row - 12, J);
            end loop;
         end loop;
      end if;

      declare
         Coefficients : constant Vector := -- MLR.
           Regression_Coefficients
             (Source => RData, Regressors => Factors_Matrix);
         K : Integer := 4;
      begin
         if Coefficients'Length = 0 then
            Singular := True;
         else
            DALEVEL := Coefficients (1);
            DAK0 := Coefficients (2);
            for I in 1 .. NM loop  -- if odd
               DALTAP (K / 2 - 1).Amplitude :=
                 Sqrt
                   (Coefficients (K - 1) * Coefficients (K - 1) +
                    Coefficients (K) * Coefficients (K));
               DALTAP (K / 2 - 1).Phase :=
                 Arctan (Coefficients (K), Coefficients (K - 1));
               K := K + 2;
            end loop;
            if Trend then
               Annual.Semi1 := Coefficients (Num_Coefficients - 2);
               Annual.Semi2 := Coefficients (Num_Coefficients - 3);
               Annual.Ann1 := Coefficients (Num_Coefficients - 4);
               Annual.Ann2 := Coefficients (Num_Coefficients - 5);
               Secular_Trend := Coefficients (Num_Coefficients - 1); --!!!
               Accel := Coefficients (Num_Coefficients); --!!!
            else
               Secular_Trend := 0.0;
               Accel := 0.0;
            end if;
            Singular := False;
         end if;
      end;
   end Regression_Factors;

   procedure Put
     (Value : in Long_Float; Text : in String := "";
      New_Line : in Boolean := False)
   is
   begin
      Ada.Long_Float_Text_IO.Put (Value, Fore => 4, Aft => 11, Exp => 0);
      Text_IO.Put (Text);
      if New_Line then
         Text_IO.New_Line;
      end if;
   end Put;

   function Filter9Point (Raw : in Data_Pairs) return Data_Pairs is
      Res : Data_Pairs := Raw;
   begin
      for I in Raw'First + 1 .. Raw'Last - 1 loop -- 4
         Res (I).Value :=
           0.25 * (+Raw (I - 1).Value) + 0.5 * (+Raw (I).Value) +
           0.25 * (+Raw (I + 1).Value);
      end loop;
      return Res;
   end Filter9Point;

end GEM.LTE.Primitives;
